@echo off
::
::  filename:  longest.bat - a batch file for MS-DOS
::    author:  Eric Pement - pemente@northpark.edu
::      date:  24 March 2001
::   purpose:  to display the longest line in a file
::  requires:  GNU awk on the PATH, file "longest.awk" in C:\BATCH

if "%1"=="/h" goto syntax
if "%1"=="/H" goto syntax
if "%1"=="/?" goto syntax
if "%1"==""   goto filter
if not exist %1 goto nofile

:: else, we must have at least one argument here

awk -f C:\BATCH\longest.awk %1 %2 %3 %4 %5 %6 %7 %8 %9
goto end

:filter
awk -f C:\BATCH\longest.awk
goto end

:nofile
echo Input file [%1] not found.
echo.

:syntax
echo LONGEST.BAT  -  A program to locate the longest line in a textfile.
echo.
echo Usage:
echo   {cat,grep,find,etc.} file %"%|%"% longest   # use as a filter (use pipe)
echo   longest file [file2 file3 ...]        # use with one argument
echo   longest /?  or  longest /h            # brings up this help msg
echo.
echo   If more than one filename is given, the longest line of all
echo   the files will be output; line count will be totalled.
:end

