# Filename: endnoteH.awk
#   Author: Eric Pement - pemente@northpark.edu
#  Version: 1.0
#     Date: May 22, 2001
#  Purpose: To convert in-text notes and references to endnotes
# Requires: GNU awk; blank lines between paragraphs of input file
#    Usage: awk -f endnoteH.awk yourfile.txt [>output.txt]
#   Output: Based on endnote.awk, semi-modified to generate HTML.
# See also: ENDNOTE.TXT, which further explains usage and application.
#    To do: check for mismatched "[[" and "]]" braces.
#
#  Credits: The idea for this endnote system was borrowed from "wsNOTE"
#           by Eric Meyer, a 1988 MS-DOS utility for managing footnotes
#           in WordStar files. His program offered many extra features.

BEGIN { a=b=0; str = "" }  # Initialize some variables

/^\[\[$/,/^]]$/ {          # If between [[ and ]] markers ..
  if ( /^\[\[$/) next      # .. skip the [[ marker,
  sub( /^]]$/, "")         # .. change the ]] marker to a blank line,

  # increment each "##" to the next variable 'a', one per line.
  sub(/\#\#/, "<B><A HREF=\"#T" ++a "\" NAME=\"E" a "\">" a "</A></B>")
  # parses out to this: <B><A HREF="#T123" NAME="E123">123</A></B>

  str = str $0 "\n"        # .. append the line to 'str' string variable
  next                     # .. get next input line (works like ELSE)
}

{                          # If not between the [[ and ]] markers ..
                           # .. increment each "##" to next variable 'b'
  gsub(/\#\#/,
  "<SUP><SMALL><A HREF=\"#E" ++b "\" NAME=\"T" b "\">" b "</A></SMALL></SUP>")
  # parses to: <SUP><SMALL><A HREF="#E123" NAME="T123">123</A></SMALL></SUP>

  print                    # .. and print that line.
}

END {                      # Error checking.
  if ( a != b ) {          # Do nums match? "\a\a" beeps the console
    print "\a\a\n\n\n",
    "================\n     WARNING!\n ================\n",
    "Body text numbers and Endnote numbers don't match!\n",
    "The Endnote section will not be printed.\n\n\n" > "/dev/stderr"
  } else {
    print "<HR width="50%"><BR>\n<B>ENDNOTES:</B><BR>\n"
    print str
    print "[end of file]<BR>"
  }
}
#---end of script---
